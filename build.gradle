plugins{
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

mainClassName = 'edu.unm.dragonfly.DashboardApplication'

group 'edu.unm.dragonfly'
version '1.0.0-SNAPSHOT'

java{
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://esri.jfrog.io/artifactory/arcgis'
    }
    maven {
        url 'https://olympus.esri.com/artifactory/arcgisruntime-repo'
    }
}

ext {
    arcgisVersion = '100.13.0'
}

configurations {
    natives
}

dependencies {

    implementation "com.esri.arcgisruntime:arcgis-java:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-jnilibs:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-resources:$arcgisVersion"

    implementation 'com.google.inject:guice:5.0.1'
    annotationProcessor 'com.google.auto.value:auto-value:1.9'
    implementation 'com.google.auto.value:auto-value-annotations:1.9'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxjavafx:2.2.2'

    implementation 'org.controlsfx:controlsfx:11.1.1'

    implementation 'edu.brown.cs.burlap:java_rosbridge:2.0.3-SNAPSHOT'

    testImplementation 'junit:junit:4.13.2'
}

task copyNatives(type: Copy) {
    description = "Copies the arcgis native libraries into the project build directory for development."
    group = "build"
    configurations.natives.asFileTree.each {
        from(zipTree(it))
    }
    // store native libraries in a common location shared with other samples
    into "${System.properties.getProperty("user.home")}/.arcgis/$arcgisVersion"
}

run {
    dependsOn copyNatives
}